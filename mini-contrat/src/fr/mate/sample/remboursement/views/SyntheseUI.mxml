<?xml version="1.0"?>
<mx:VBox xmlns:mx="http://www.adobe.com/2006/mxml"
		 xmlns="fr.mate.sample.remboursement.views.*"
		 width="100%">

	<mx:Script>
		<![CDATA[
			import fr.mate.sample.model.*;
			import fr.mate.sample.remboursement.model.*;
			import fr.mate.sample.remboursement.views.*;
			import fr.mate.core.*;
			import mx.events.*;
			
			/** Permet de savoir si le model a été initialisé. */
			private var initModel:Boolean = false;
			
			/** View model. */
			private var _viewModel:SyntheseVM;
			
			/** View model */
			[Bindable]
			public function get viewModel():SyntheseVM
			{
				return _viewModel;
			}
			
			/** View mode et initialisation */
			public function set viewModel(vm:SyntheseVM):void
			{
				if (!initModel)
				{
					initModel = true;
					
					_viewModel = vm;
					
					if (_viewModel.annees != null)
					{
						creerOnglets(PropertyChangeEvent.createUpdateEvent(this, "annees", null, _viewModel.annees));
					}
					
					_viewModel.addEventListener(PropertyChangeEvent.PROPERTY_CHANGE, creerOnglets);
				}
			}
			
			/**
			 * Création des onglets pour les années qui ont été créées lors du chargement du contrat.
			 * @param	event Event
			 */
			private function creerOnglets(event:PropertyChangeEvent):void
			{
				if (event.property == "annees")
				{
					for each (var annee:Number in viewModel.annees)
					{
						var onglet:OngletRemboursement = new OngletRemboursement();
						onglet.label = annee.toString();
						onglet.viewModel = viewModel;
						
						tabAnnees.addChild(onglet);
					}
					
					trace("Nombre d'onglets crées : " + tabAnnees.getChildren().length);
				}
			}
			
			private function changerAnnee(event:IndexChangedEvent):void
			{
				var annee:Number = parseInt(OngletRemboursement(tabAnnees.getChildAt(event.newIndex)).label);
				
				viewModel.chargerRemboursement(viewModel.contratSelectionne.id, annee);
			}
		]]>
	</mx:Script>

	<ContratInfo contrat="{viewModel.contratSelectionne}" />

	<mx:TabNavigator id="tabAnnees"
					 change="changerAnnee(event)"
					 width="100%" />
	
</mx:VBox>
